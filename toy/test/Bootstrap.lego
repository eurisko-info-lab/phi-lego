-----------------------------------------------------
-- Bootstrap: The Meta-Grammar
--
-- This is the grammar that parses grammars.
-- Self-describing: it defines the syntax used to write itself.
--
-- Two levels of grammar:
--   token: CharStream â†’ Token (lexer level)
--   piece: TokenStream â†’ Term (parser level)
--
-- NOTE ON CHARACTER CLASSES:
-- The character classes below define what the tokenizer recognizes.
-- In a production implementation, the tokenizer should use Unicode
-- categories (IsLetter, IsDigit, etc.) rather than enumeration.
-- This enumeration exists only because the current implementation
-- requires explicit character lists.
--
-- Users should be able to use ANY Unicode in their .lego files.
-- The limitation is in the implementation, not the design.
-----------------------------------------------------

lang Bootstrap :=

-----------------------------------------------------
-- Token: Character-level grammar (lexer)
-- Uses 'x' single-char literals, same algebra as piece
--
-- FUTURE: Replace character enumeration with Unicode categories:
--   alpha ::= <unicode-letter> | '_' ;
--   digit ::= <unicode-digit> ;
--   symch ::= <unicode-symbol> | <unicode-punctuation> ;
-----------------------------------------------------

token Token
  -- Basic ASCII character classes
  digit  ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' ;
  lower  ::= 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j'
           | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't'
           | 'u' | 'v' | 'w' | 'x' | 'y' | 'z' ;
  upper  ::= 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J'
           | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T'
           | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z' ;
  
  -- Extended characters (implementation limitation - should be Unicode categories)
  -- Greek letters for type theory
  greek  ::= 'Î±' | 'Î²' | 'Î³' | 'Î´' | 'Îµ' | 'Î¶' | 'Î·' | 'Î¸' | 'Î¹' | 'Îº'
           | 'Î»' | 'Î¼' | 'Î½' | 'Î¾' | 'Î¿' | 'Ï€' | 'Ï' | 'Ïƒ' | 'Ï„' | 'Ï…'
           | 'Ï†' | 'Ï‡' | 'Ïˆ' | 'Ï‰'
           | 'Î‘' | 'Î’' | 'Î“' | 'Î”' | 'Î•' | 'Î–' | 'Î—' | 'Î˜' | 'Î™' | 'Îš'
           | 'Î›' | 'Îœ' | 'Î' | 'Î' | 'ÎŸ' | 'Î ' | 'Î¡' | 'Î£' | 'Î¤' | 'Î¥'
           | 'Î¦' | 'Î§' | 'Î¨' | 'Î©' ;
  -- Double-struck (blackboard bold) letters
  mathbb ::= 'ğ•€' | 'ğ”½' | 'ğ•Š' ;
  -- Superscripts and subscripts
  super  ::= 'Â¹' | 'Â²' | 'Â³' ;
  sub    ::= 'â‚€' | 'â‚' | 'â‚‚' | 'â‚ƒ' | 'â‚„' | 'â‚…' | 'â‚†' | 'â‚‡' | 'â‚ˆ' | 'â‚‰' ;
  -- Math relation and operator symbols
  mathrel ::= 'â‰ˆ' | 'â‰…' | 'â‰¤' | 'â‰¥' | 'â‰¡' ;
  mathop  ::= 'âˆ˜' | 'âŠ—' | 'âŠ™' | 'âˆ§' | 'âˆ¨' | 'âˆ·' | 'â‹†' | 'Â¯' | 'âŠ¤' | 'âŠ¥' ;
  
  -- Alphabetic: anything that can start/continue an identifier
  alpha  ::= lower | upper | greek | mathbb | super | sub | mathrel | mathop | '_' ;
  
  -- Symbol characters (punctuation, operators)
  symch  ::= '(' | ')' | '[' | ']' | '{' | '}' | '<' | '>'
           | ':' | ';' | ',' | '.' | '|' | '!' | '?' | '@'
           | '#' | '$' | '%' | '^' | '&' | '*' | '+' | '-'
           | '=' | '~' | '/' | '\\' | 'â†’' | 'â†' | 'â†”' | 'âŠ•'
           | 'âŠ¢' | 'Ã—' | 'Î»' | 'âˆ‚' | 'âˆ€' | 'âˆƒ' | 'â¦‰' | 'â¦Š'
           | 'Â«' | 'Â»' | 'â˜…' | 'â˜†' | '`' | 'â†¦' | 'â†¾'
           | 'âŸ¨' | 'âŸ©' ;

  -- Token patterns
  -- Note: '/' is allowed in identifiers for hierarchical names (e.g., trans/filler in redtt)
  -- Note: '\'' is allowed as a suffix for primed identifiers (e.g., symm' in redtt)
  ident   ::= alpha (alpha | digit | '-' | '/' | '\'')* ;
  number  ::= digit digit* ;
  string  ::= '"' strchar* '"' ;
  strchar ::= '\\' escape | printable ;
  escape  ::= '"' | '\\' | 'n' | 't' | 'r' | '\'' ;
  printable ::= alpha | digit | symch | ' ' ;
  -- Character literals: 'x' or '\x' (including '\'' for escaped quote)
  char    ::= '\'' charinner '\'' ;
  charinner ::= '\\' escape | alpha | digit | symch | ' ' | '"' ;

  -- Whitespace and comments (to skip)
  ws      ::= ' ' | '\t' | '\n' | '\r' ;
  comment ::= '-' '-' nonnl* ;
  nonnl   ::= alpha | digit | symch | ' ' | '\t' | '\'' | '"' ;

  -- Multi-character operators (ordered longest first)
  op3     ::= ':' ':' '='    -- ::=
            | '=' 'I' '=' ;  -- =I=
  op2     ::= '~' '~' '>'    -- ~~>
            | ':' '='        -- :=
            | '~' '>'        -- ~>
            | '-' '>'        -- ->
            | '<' '-'        -- <-
            | '=' '>'        -- =>
            | '@' '@' ;      -- @@

  -- Special <name> syntax
  special ::= '<' alpha+ '>' ;

  -- Single symbol (fallback)
  sym     ::= symch ;

-----------------------------------------------------
-- Atoms: Basic tokens (references Token piece outputs)
-----------------------------------------------------

piece Atom
  ident  ::= <ident> â†’ ident ;
  string ::= <string> â†’ string ;
  char   ::= <char> â†’ char ;
  number ::= <number> â†’ number ;
  -- Meta-type aliases for generated grammars
  expr   ::= <expr> â†’ expr ;
  symbol ::= <symbol> â†’ symbol ;
  bool   ::= <bool> â†’ bool ;
  elabCtx ::= <elabCtx> â†’ elabCtx ;
  sysEntry ::= <sysEntry> â†’ sysEntry ;
  visibility ::= <visibility> â†’ visibility ;
  name   ::= <name> â†’ name ;

-----------------------------------------------------
-- Term: S-expression terms
-- Binder notation: `ident "." term` binds identifier in term
-----------------------------------------------------

piece Term
  term    ::= "$" ident "." term â†’ binder
            | "$" ident â†’ var
            | ident "." term â†’ binder
            | ident â†’ var
            | "_" â†’ wildcard
            | string â†’ lit
            | number â†’ num
            | "(" "Î " "(" ident ":" term ")" "." term ")" â†’ piType
            | "(" "Î " "(" "_" ":" term ")" "." term ")" â†’ piType
            | "(" "Î£" "(" ident ":" term ")" "." term ")" â†’ sigmaType
            | "(" "Î£" "(" "_" ":" term ")" "." term ")" â†’ sigmaType
            | "[" term ":=" term "]" term â†’ subst
            | "[" term ":" term "]" term â†’ typedCtx
            | "[" term "]" â†’ bracket
            | "{" termArg* "}" â†’ brace
            | "(" "[" term ":=" term "]" term ")" â†’ parenSubst
            | "(" "[" term ":" term "]" term ")" â†’ parenTypedCtx
            | "(" term ":" term ")" â†’ ann
            | "(" "$" ident "." term ")" â†’ binderParen
            | "(" ident "." term ")" â†’ objBinderParen
            | "(" conname "$" ident ":" term "." term ")" â†’ binderCon
            | "(" conname ident ":" term "." term ")" â†’ binderCon
            | "(" "(" conname termArg* ")" termArg+ ")" â†’ app
            | "(" conname termArg* ")" â†’ con ;
  -- termArg: arguments in term applications, supports typed vars ($x : A)
  termArg ::= "$" ident "." term â†’ binder
            | "$" ident ":" term â†’ typedVar
            | "$" ident ":=" term â†’ assign
            | ident ":=" term â†’ assign
            | ident ":" term â†’ labeledArg
            | ident "." term â†’ objBinder
            | "~>" â†’ con
            | "â†’" â†’ con
            | "Ã—" â†’ con
            | "â†¦" â†’ con
            | "=I=" â†’ con
            | "@@" â†’ con
            | "-" â†’ con
            | "â†¾" â†’ con
            | "@" â†’ con
            | "." â†’ con
            | "|" â†’ con
            | "=>" â†’ con
            | "â†" â†’ con
            | "with" â†’ con
            | "where" â†’ con
            | term ;
  -- conname: constructor names can be idents or reserved keywords (as symbols)
  -- Multi-char operators that can appear as constructor heads
  conname ::= "=I=" | "@@" | "~>" | "â†’" | "Ã—" | "â†¦" | "âŸ¨" | "âŸ©" | ident | <sym> ;

-----------------------------------------------------
-- Pattern: Match patterns for rules
-- Produces clean Terms: $x â†’ (var "$x"), (f x) â†’ (con "f" [...])
-- Binder notation: $x . body binds x in body
-----------------------------------------------------

piece Pattern
  pattern ::= "$" ident "." pattern â†’ binder
            | "$" ident â†’ var
            | "[" pattern ":=" pattern "]" pattern â†’ subst
            | "[" patternArg* "]" â†’ bracket
            | "{" patternArg* "}" â†’ brace
            | "(" pattern "," pattern ")" â†’ tuple
            | "(" ")" â†’ unit
            | "(" "$" ident "." pattern ")" â†’ binderParen
            | "(" ident "." pattern ")" â†’ objBinderParen
            | "(" pattern ":" pattern ")" â†’ ann
            | "(" "(" "(" patternHead patternArg* ")" ")" patternArg+ ")" â†’ app
            | "(" "(" patternHead patternArg* ")" patternArg+ ")" â†’ app
            | "(" "(" "[" patternArg* "]" ")" patternArg+ ")" â†’ app
            | "(" "[" patternArg* "]" patternArg+ ")" â†’ app
            | "(" "(" pattern ":" pattern ")" ")" â†’ ann
            | "(" patternHead patternArg* ")" â†’ con
            | string â†’ lit
            | number â†’ num
            | ident â†’ con ;
  patternArg ::= "$" ident "." pattern â†’ binder
               | "$" ident ":" pattern â†’ typedVar
               | "$" ident ":=" pattern â†’ assign
               | ident ":=" pattern â†’ assign
               | ident ":" pattern â†’ labeledArg
               | ident "." pattern â†’ objBinder
               | "~>" â†’ con
               | "â†’" â†’ con
               | "Ã—" â†’ con
               | "â†¦" â†’ con
               | "=I=" â†’ con
               | "@@" â†’ con
               | "-" â†’ con
               | "â†¾" â†’ con
               | "@" â†’ con
               | "." â†’ con
               | "|" â†’ con
               | "=>" â†’ con
               | "â†" â†’ con
               | "âŸ¨" â†’ con
               | "âŸ©" â†’ con
               | "," â†’ con
               | ":" â†’ con
               | ":=" â†’ con
               | "=" â†’ con
               | "in" â†’ con
               | "with" â†’ con
               | "where" â†’ con
               | pattern ;
  patternHead ::= "$" ident â†’ var
                | number â†’ num
                | conname ;

-----------------------------------------------------
-- Template: Substitution templates for rules
-- Same structure as Pattern - produces clean Terms
-- Binder notation: $x . body binds x in body
-----------------------------------------------------

piece Template
  template ::= "$" ident "." template â†’ binder
             | "$" ident â†’ var
             | "let" ident "=" template "in" template â†’ letIn
             | "let" "$" ident "=" template "in" template â†’ letIn
             | "let" "(" template "," template ")" "=" template "in" template â†’ letTupleIn
             | "case" template caseArm+ â†’ caseExpr
             | "[" template ":=" template "]" template â†’ subst
             | "[" templateArg* "]" â†’ bracket
             | "{" templateArg* "}" â†’ brace
             | "(" template "," template ("," template)+ ")" â†’ nTuple
             | "(" template "," template ")" â†’ tuple
             | "(" ")" â†’ unit
             | "(" "case" template caseArm+ ")" â†’ caseExpr
             | "(" "$" ident "." template ")" â†’ binderParen
             | "(" ident "." template ")" â†’ objBinderParen
             | "(" "Î»" ident "." template ")" â†’ lamParen
             | "(" ident ident "." headTerm templateArg* ")" â†’ introExpr
             | "(" "let" ident "=" template "in" template ")" â†’ letIn
             | "(" "let" "$" ident "=" template "in" template ")" â†’ letIn
             | "(" "let" "(" template "," template ")" "=" template "in" template ")" â†’ letTupleIn
             | "(" headTerm templateArg* ")" â†’ con
             | "(" ident ":" template templateArg+ ")" â†’ record
             | "(" template ":" template ")" â†’ typed
             | "(" template ")" â†’ paren
             | ident parenArg+ ident+ â†’ con
             | ident parenArg+ â†’ con
             | string â†’ lit
             | number â†’ num
             | ident â†’ con ;
  parenArg ::= "(" template ")" â†’ paren
             | "(" template "," template ("," template)+ ")" â†’ nTuple
             | "(" template "," template ")" â†’ tuple
             | "(" ")" â†’ unit
             | "(" headTerm templateArg* ")" â†’ con
             | "(" template ":" template ")" â†’ typed ;
  caseArm ::= "(" patternHead patternArg* ")" "=>" template â†’ arm
            | "$" ident "=>" template â†’ varArm
            | ident "=>" template â†’ arm
            | conname "=>" template â†’ arm ;
  templateArg ::= "$" ident "." template â†’ binder
                | "$" ident ":" template â†’ typedVar
                | "$" ident ":=" template â†’ assign
                | ident ":=" template â†’ assign
                | ident ":" template â†’ labeledArg
                | string ":" template â†’ stringLabeledArg
                | "(" template ":" template ")" â†’ typed
                | ident "." template â†’ objBinder
                | "~>" â†’ con
                | "â†’" â†’ con
                | "Ã—" â†’ con
                | "â†¦" â†’ con
                | "=I=" â†’ con
                | "@@" â†’ con
                | "-" â†’ con
                | "â†¾" â†’ con
                | "@" â†’ con
                | "." â†’ con
                | "|" â†’ con
                | "=>" â†’ con
                | "â†" â†’ con
                | "âŸ¨" â†’ con
                | "âŸ©" â†’ con
                | "," â†’ con
                | "with" â†’ con
                | "where" â†’ con
                | template ;
  headTerm ::= "$" ident â†’ var
             | ident
             | string
             | number
             | "(" template ":" template ")" â†’ typed
             | "(" template "," template ("," template)+ ")" â†’ nTuple
             | "(" template "," template ")" â†’ tuple
             | "(" template templateArg* ")" â†’ app ;

-----------------------------------------------------
-- GrammarExpr: Grammar expressions
-----------------------------------------------------

piece GrammarExpr
  expr    ::= alt ;
  alt     ::= seq "|" alt â†’ alt
            | seq ;
  seq     ::= seqBase "â†’" ident â†’ annotated
            | seqBase ;
  seqBase ::= suffix+ â†’ seq
            | suffix ;
  suffix  ::= atom "*" â†’ star
            | atom "+" â†’ plus
            | atom "?" â†’ opt
            | atom ;
  atom    ::= string â†’ lit
            | char â†’ chr
            | ident â†’ ref
            | "(" expr ")" â†’ group
            | <special> â†’ special
            | "Îµ" â†’ empty ;

-----------------------------------------------------
-- File: Top-level declarations
-----------------------------------------------------

piece File
  legoFile   ::= decl* ;
  decl       ::= importDecl | langDecl | tokenDecl | pieceDecl | vocabDecl | ruleDecl | typeDecl | testDecl | attrsDecl ;
  importDecl ::= "import" modulePath ";"? â†’ DImport ;
  vocabDecl  ::= "vocab" ident ident* â†’ DVocab ;
  modulePath ::= ident ("." ident)* â†’ modulePath ;
  langDecl   ::= "lang" ident imports? ":=" langBody â†’ DLang ;
  imports    ::= "(" ident ("," ident)* ")" â†’ DImports
               | "(" ident+ ")" â†’ DImports ;
  langBody   ::= innerDecl* ;
  innerDecl  ::= tokenDecl | pieceDecl | ruleDecl | typeDecl | testDecl | attrsDecl 
               | deriveDecl | algebraDecl | effectDecl | opticsDecl | adjunctionDecl 
               | kanDecl | operadDecl | natDecl | macroDecl | lawsDecl ;
  tokenDecl  ::= "token" ident tokenItem+ â†’ DToken ;
  tokenItem  ::= prodDecl ;
  pieceDecl  ::= "piece" ident pieceItem+ â†’ DPiece ;
  pieceItem  ::= prodDecl | ruleDecl | typeDecl | testDecl | deriveDecl | algebraDecl 
               | effectDecl | opticsDecl | adjunctionDecl | kanDecl | operadDecl | natDecl 
               | macroDecl | lawsDecl ;
  prodDecl   ::= ident "::=" expr ";" â†’ DGrammar ;
  ruleDecl   ::= "rule" ident ":"? pattern ruleArrow template ruleGuard? ";" â†’ DRule ;
  ruleArrow  ::= "~~>" | "~>" ;
  ruleGuard  ::= "when" sideCondition ("," sideCondition)* â†’ guard ;
  -- Derive operations from grammar (algebraic recursion schemes)
  deriveDecl ::= "derive" deriveKind "for" ident deriveWith? ";" â†’ DDerive ;
  deriveKind ::= "subst" â†’ DSubst | "shift" â†’ DShift | "map" â†’ DMap | "fold" â†’ DFold
               | "cata" â†’ DCata | "ana" â†’ DAna | "hylo" â†’ DHylo | "para" â†’ DPara
               | "conv" â†’ DConv | "eq" â†’ DEq | "infer" â†’ DInfer | "check" â†’ DCheck
               | "normalize" â†’ DNormalize | "eval" â†’ DEval ;
  deriveWith ::= "with" deriveMod ("," deriveMod)* â†’ deriveWith ;
  deriveMod  ::= "binders" "=" "[" ident ("," ident)* "]" â†’ binderList
               | "fuel" "=" <number> â†’ fuelMod
               | "base" "=" pattern â†’ baseMod ;
  -- Macro: syntactic abbreviation (definitional expansion)
  macroDecl  ::= "macro" ident macroParams? ":=" expr ";" â†’ DMacro ;
  macroParams ::= ident+ â†’ macroParams ;
  -- Algebra laws: named equations for a sort
  lawsDecl   ::= "algebra" ident "for" ident "{" lawItem* "}" â†’ DAlgebra ;
  lawItem    ::= ident ":" expr "=" expr ";" â†’ lawEq
               | ident ":" expr "=" expr "," expr "=" expr ";" â†’ lawEq2 ;
  -- Grammar algebra: pushout, pullback, quotient
  algebraDecl ::= "pushout" ident ident "along" ident ";" â†’ DPushout
                | "pullback" ident ident "over" ident ";" â†’ DPullback
                | "quotient" ident "by" ident ";" â†’ DQuotient
                | "initial" ident ";" â†’ DInitial
                | "terminal" ident ";" â†’ DTerminal
                | "coproduct" ident ident ";" â†’ DCoproduct
                | "product" ident ident ";" â†’ DProduct
                | ident "extends" ident extendWith? ";" â†’ DExtends ;
  extendWith ::= "with" extendMod ("," extendMod)* â†’ extendWith ;
  extendMod  ::= "rename" ident "to" ident â†’ renameMod
               | "hide" ident â†’ hideMod
               | "override" ident "=" expr â†’ overrideMod ;
  -- Effect algebra: monads, applicatives, free constructions
  effectDecl ::= "effect" ident effectBody â†’ DEffect
               | "handler" ident "for" ident effectBody â†’ DHandler
               | "free" ident "over" ident ";" â†’ DFree
               | "cofree" ident "over" ident ";" â†’ DCofree
               | "monad" ident "=" monadSpec ";" â†’ DMonad ;
  effectBody ::= "{" effectOp* "}" ;
  effectOp   ::= "op" ident ":" term "â†’" term ";" â†’ DOp
               | "return" ":" term ";" â†’ DReturn
               | "bind" ":" term ";" â†’ DBind ;
  monadSpec  ::= "State" ident â†’ stateMonad
               | "Reader" ident â†’ readerMonad
               | "Writer" ident â†’ writerMonad
               | "Error" ident â†’ errorMonad
               | "IO" â†’ ioMonad
               | monadSpec "Ã—" monadSpec â†’ productMonad
               | monadSpec "âˆ˜" monadSpec â†’ composeMonad ;
  -- Optics: bidirectional transformations (van Laarhoven / profunctor)
  opticsDecl ::= "lens" ident ":" ident "âŸ·" ident ";" â†’ DLens
               | "prism" ident ":" ident "âŸ·" ident ";" â†’ DPrism
               | "iso" ident ":" ident "â‰…" ident ";" â†’ DIso
               | "traversal" ident ":" ident "âŸ¿" ident ";" â†’ DTraversal
               | "affine" ident ":" ident "â¤³" ident ";" â†’ DAffine
               | "getter" ident ":" ident "â†’" ident ";" â†’ DGetter
               | "setter" ident ":" ident "â†" ident ";" â†’ DSetter
               | "review" ident ":" ident "â†©" ident ";" â†’ DReview ;
  -- Adjunctions: Free âŠ£ Forgetful (generates both + unit/counit)
  adjunctionDecl ::= ident "âŠ£" ident ":" ident "â‡„" ident ";" â†’ DAdjunction
                   | "forgetful" ident ":" ident "â†’" ident ";" â†’ DForgetful
                   | "left" "adjoint" ident "to" ident ";" â†’ DLeftAdj
                   | "right" "adjoint" ident "to" ident ";" â†’ DRightAdj ;
  -- Kan extensions: the universal property
  kanDecl ::= "lan" ident "along" ident ";" â†’ DLan        -- Left Kan extension
            | "ran" ident "along" ident ";" â†’ DRan        -- Right Kan extension
            | "yoneda" ident ";" â†’ DYoneda                -- Yoneda embedding
            | "coYoneda" ident ";" â†’ DCoYoneda            -- Density comonad
            | "codensity" ident ";" â†’ DCodensity          -- Codensity monad
            | "density" ident ";" â†’ DDensity ;            -- Density comonad
  -- Operads: multi-arity composition
  operadDecl ::= "operad" ident operadBody â†’ DOperad
               | "algebra" ident "over" ident ";" â†’ DOperadAlg ;
  operadBody ::= "{" operadOp* "}" ;
  operadOp   ::= "arity" <number> ":" ident ";" â†’ DArity
               | "compose" ":" ident ";" â†’ DCompose
               | "unit" ":" ident ";" â†’ DUnit ;
  -- Natural transformations: polymorphic functions between functors
  natDecl ::= ident "=>" ident ":" ident "âŸ¹" ident ";" â†’ DNat
            | "dinatural" ident ":" ident "â¤‡" ident ";" â†’ DDinat
            | "extranatural" ident ":" ident "âŸ¾" ident ";" â†’ DExtranat ;
  typeDecl   ::= "type" ident ":" pattern ":" term whenClause? ";" â†’ DType ;
  whenClause ::= "when" typeConstraint ("," typeConstraint)* â†’ whenClause ;
  typeConstraint ::= pattern ":" term â†’ constraint
                   | "[" pattern ":" term "]" pattern ":" term â†’ bracketConstraint
                   | "[" pattern ":" term ("," pattern ":" term)+ "]" pattern ":" term â†’ multiBracketConstraint
                   | "[" pattern ":=" pattern "]" pattern "=" term â†’ substConstraint
                   | "[" pattern "=" pattern "]" pattern ":" term â†’ eqCtxConstraint
                   | "[" pattern "]" pattern ":" term â†’ ctxConstraint
                   | "[" pattern "]" term "=" term â†’ eqConstraint
                   | sideCondition ;
  sideCondition ::= pattern "not" "free" "in" pattern â†’ notFree
                  | pattern "agree" "on" "overlaps" â†’ agreeOverlaps
                  | pattern "agrees" pattern â†’ agrees
                  | pattern "boundary" pattern â†’ boundary
                  | pattern "holds" â†’ holds
                  | pattern "declared" â†’ declared
                  | "bound" pattern ":" pattern â†’ bound
                  | pattern "=" pattern â†’ eq
                  | pattern â†’ sidePattern ;
  testDecl   ::= "test" string ":" annotatedTerm ("~~>" annotatedTerm)? ";" â†’ DTest ;
  annotatedTerm ::= term ":" term â†’ ann | term ;
  -- Attribute Grammar section (groups syn/inh declarations and rules)
  attrsDecl  ::= "attrs" ident attrBody â†’ DAttrs ;
  attrBody   ::= attrItem* ;
  attrItem   ::= attrDecl | attrRuleDecl ;
  attrDecl   ::= attrFlow ident ":" term ";" â†’ DAttr ;
  attrFlow   ::= "syn" â†’ syn | "inh" â†’ inh ;
  attrRuleDecl ::= attrPath "=" term ";" â†’ DAttrRule ;
  attrPath   ::= ident ("." ident)* â†’ attrPath ;

-----------------------------------------------------
-- Interpreter Rules: Combining sequences
-- These define how the parser combines parsed terms
-----------------------------------------------------

-- combineSeq: Combine two terms into a sequence
-- seq + seq â†’ merge lists
rule combine-seq-seq: (combineSeq (seq $ts1) (seq $ts2)) ~~> (seq (append $ts1 $ts2)) ;
-- seq + other â†’ append to seq
rule combine-seq-left: (combineSeq (seq $ts) $t) ~~> (seq (append $ts (list $t))) ;
-- other + seq â†’ prepend to seq  
rule combine-seq-right: (combineSeq $t (seq $ts)) ~~> (seq (cons $t $ts)) ;
-- unit is identity
rule combine-seq-unit-l: (combineSeq (unit) $t) ~~> $t ;
rule combine-seq-unit-r: (combineSeq $t (unit)) ~~> $t ;
-- default: make a new seq
rule combine-seq-default: (combineSeq $t1 $t2) ~~> (seq (list $t1 $t2)) ;

-- splitSeq: Split first element from a sequence
rule split-seq: (splitSeq (seq (cons $h $rest))) ~~> (pair $h (seq $rest)) ;
rule split-single: (splitSeq $t) ~~> (pair $t (unit)) ;

-- wrapNode: Wrap a term with a node name
rule wrap-seq: (wrapNode $name (seq $ts)) ~~> (con $name $ts) ;
rule wrap-other: (wrapNode $name $t) ~~> (con $name (list $t)) ;

-- unwrapNode: Unwrap a node if name matches
rule unwrap-match: (unwrapNode $name (con $name $ts)) ~~> (seq $ts) ;
rule unwrap-nomatch: (unwrapNode $name $t) ~~> $t ;

-----------------------------------------------------
-- Tests (only Term syntax - not Pattern syntax)
-----------------------------------------------------

test "parse ident": x ;
test "parse string": "hello" ;
test "parse term var": x ;
test "parse term con": (f x y) ;
test "parse nested con": (app (lam x x) y) ;
