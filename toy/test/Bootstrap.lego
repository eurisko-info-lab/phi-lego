-----------------------------------------------------
-- Bootstrap: The Meta-Grammar
--
-- This is the grammar that parses grammars.
-- Self-describing: it defines the syntax used to write itself.
--
-- Two levels of grammar:
--   token: CharStream â†’ Token (lexer level)
--   piece: TokenStream â†’ Term (parser level)
-----------------------------------------------------

lang Bootstrap :=

-----------------------------------------------------
-- Token: Character-level grammar (lexer)
-- Uses 'x' single-char literals, same algebra as piece
-----------------------------------------------------

token Token
  -- Character classes
  digit  ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' | 'Â²' | 'Â³' ;
  lower  ::= 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j'
           | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't'
           | 'u' | 'v' | 'w' | 'x' | 'y' | 'z' ;
  upper  ::= 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J'
           | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T'
           | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z' ;
  greek  ::= 'Î±' | 'Î²' | 'Î³' | 'Î´' | 'Îµ' | 'Î¶' | 'Î·' | 'Î¸' | 'Î¹' | 'Îº'
           | 'Î»' | 'Î¼' | 'Î½' | 'Î¾' | 'Î¿' | 'Ï€' | 'Ï' | 'Ïƒ' | 'Ï„' | 'Ï…'
           | 'Ï†' | 'Ï‡' | 'Ïˆ' | 'Ï‰'
           | 'Î‘' | 'Î’' | 'Î“' | 'Î”' | 'Î•' | 'Î–' | 'Î—' | 'Î˜' | 'Î™' | 'Îš'
           | 'Î›' | 'Îœ' | 'Î' | 'Î' | 'ÎŸ' | 'Î ' | 'Î¡' | 'Î£' | 'Î¤' | 'Î¥'
           | 'Î¦' | 'Î§' | 'Î¨' | 'Î©' ;
  -- Double-struck (blackboard bold) letters used in type theory (ğ•€ for interval, etc)
  mathbb ::= 'ğ•€' | 'ğ”½' | 'ğ•Š' ;
  -- Math symbols used in type theory
  mathsym ::= 'â‰ˆ' | 'â‰…' | 'â‰¤' | 'â‰¥' | 'âˆ˜' | 'âŠ—' | 'âŠ™' | 'âˆ§' | 'âˆ¨' | 'âˆ·' 
            | 'â‰¡' | 'â‹†' | 'Â¯' | 'âŠ¤' | 'âŠ¥' | 'Â¹' | 'â‚€' | 'â‚' | 'â‚‚' | 'â‚ƒ' 
            | 'â‚„' | 'â‚…' | 'â‚†' | 'â‚‡' | 'â‚ˆ' | 'â‚‰' ;
  alpha  ::= lower | upper | greek | mathbb | mathsym | '_' ;
  symch  ::= '(' | ')' | '[' | ']' | '{' | '}' | '<' | '>'
           | ':' | ';' | ',' | '.' | '|' | '!' | '?' | '@'
           | '#' | '$' | '%' | '^' | '&' | '*' | '+' | '-'
           | '=' | '~' | '/' | '\\' | 'â†’' | 'â†' | 'â†”' | 'âŠ•'
           | 'âŠ¢' | 'Ã—' | 'Î»' | 'âˆ‚' | 'âˆ€' | 'âˆƒ' | 'â¦‰' | 'â¦Š'
           | 'Â«' | 'Â»' | 'â˜…' | 'â˜†' | '`' ;

  -- Token patterns
  -- Note: '/' is allowed in identifiers for hierarchical names (e.g., trans/filler in redtt)
  -- Note: '\'' is allowed as a suffix for primed identifiers (e.g., symm' in redtt)
  ident   ::= alpha (alpha | digit | '-' | '/' | '\'')* ;
  number  ::= digit digit* ;
  string  ::= '"' strchar* '"' ;
  strchar ::= '\\' escape | printable ;
  escape  ::= '"' | '\\' | 'n' | 't' | 'r' | '\'' ;
  printable ::= alpha | digit | symch | ' ' ;
  -- Character literals: 'x' or '\x' (including '\'' for escaped quote)
  char    ::= '\'' charinner '\'' ;
  charinner ::= '\\' escape | alpha | digit | symch | ' ' | '"' ;

  -- Whitespace and comments (to skip)
  ws      ::= ' ' | '\t' | '\n' | '\r' ;
  comment ::= '-' '-' nonnl* ;
  nonnl   ::= alpha | digit | symch | ' ' | '\t' | '\'' | '"' ;

  -- Multi-character operators (ordered longest first)
  op3     ::= ':' ':' '=' ;  -- ::=
  op2     ::= '~' '~' '>'    -- ~~>
            | ':' '='        -- :=
            | '~' '>'        -- ~>
            | '-' '>'        -- ->
            | '<' '-' ;      -- <-

  -- Special <name> syntax
  special ::= '<' alpha+ '>' ;

  -- Single symbol (fallback)
  sym     ::= symch ;

-----------------------------------------------------
-- Atoms: Basic tokens (references Token piece outputs)
-----------------------------------------------------

piece Atom
  ident  ::= <ident> â†’ ident ;
  string ::= <string> â†’ string ;
  char   ::= <char> â†’ char ;
  number ::= <number> â†’ number ;

-----------------------------------------------------
-- Term: S-expression terms
-----------------------------------------------------

piece Term
  term    ::= ident â†’ var
            | string â†’ lit
            | number â†’ num
            | "(" conname term* ")" â†’ con ;
  -- conname: constructor names can be idents or reserved keywords (as symbols)
  conname ::= ident | <sym> ;

-----------------------------------------------------
-- Pattern: Match patterns for rules
-- Produces clean Terms: $x â†’ (var "$x"), (f x) â†’ (con "f" [...])
-----------------------------------------------------

piece Pattern
  pattern ::= "$" ident â†’ var
            | "(" conname pattern* ")" â†’ con
            | string â†’ lit
            | ident â†’ con ;

-----------------------------------------------------
-- Template: Substitution templates for rules
-- Same structure as Pattern - produces clean Terms
-----------------------------------------------------

piece Template
  template ::= "$" ident â†’ var
             | "(" ident template* ")" â†’ con
             | string â†’ lit
             | ident â†’ con ;

-----------------------------------------------------
-- GrammarExpr: Grammar expressions
-----------------------------------------------------

piece GrammarExpr
  expr    ::= alt ;
  alt     ::= seq "|" alt â†’ alt
            | seq ;
  seq     ::= seqBase "â†’" ident â†’ annotated
            | seqBase ;
  seqBase ::= suffix+ â†’ seq
            | suffix ;
  suffix  ::= atom "*" â†’ star
            | atom "+" â†’ plus
            | atom "?" â†’ opt
            | atom ;
  atom    ::= string â†’ lit
            | char â†’ chr
            | ident â†’ ref
            | "(" expr ")" â†’ group
            | <special> â†’ special
            | "Îµ" â†’ empty ;

-----------------------------------------------------
-- File: Top-level declarations
-----------------------------------------------------

piece File
  legoFile   ::= decl* ;
  decl       ::= importDecl | langDecl | tokenDecl | pieceDecl | ruleDecl | testDecl | attrsDecl ;
  importDecl ::= "import" ident ";" â†’ DImport ;
  langDecl   ::= "lang" ident imports? ":=" langBody â†’ DLang ;
  imports    ::= "(" ident ("," ident)* ")" â†’ DImports ;
  langBody   ::= innerDecl* ;
  innerDecl  ::= tokenDecl | pieceDecl | ruleDecl | testDecl | attrsDecl ;
  tokenDecl  ::= "token" ident prodDecl+ â†’ DToken ;
  pieceDecl  ::= "piece" ident prodDecl+ â†’ DPiece ;
  prodDecl   ::= ident "::=" expr ";" â†’ DGrammar ;
  ruleDecl   ::= "rule" ident ":" pattern "~>" template ";" â†’ DRule ;
  testDecl   ::= "test" string ":" term ("~~>" term)? ";" â†’ DTest ;
  -- Attribute Grammar section (groups syn/inh declarations and rules)
  attrsDecl  ::= "attrs" ident attrBody â†’ DAttrs ;
  attrBody   ::= attrItem* ;
  attrItem   ::= attrDecl | attrRuleDecl ;
  attrDecl   ::= attrFlow ident ":" term ";" â†’ DAttr ;
  attrFlow   ::= "syn" â†’ syn | "inh" â†’ inh ;
  attrRuleDecl ::= attrPath "=" term ";" â†’ DAttrRule ;
  attrPath   ::= ident ("." ident)* â†’ attrPath ;

-----------------------------------------------------
-- Interpreter Rules: Combining sequences
-- These define how the parser combines parsed terms
-----------------------------------------------------

-- combineSeq: Combine two terms into a sequence
-- seq + seq â†’ merge lists
rule combine-seq-seq: (combineSeq (seq $ts1) (seq $ts2)) ~> (seq (append $ts1 $ts2)) ;
-- seq + other â†’ append to seq
rule combine-seq-left: (combineSeq (seq $ts) $t) ~> (seq (append $ts (list $t))) ;
-- other + seq â†’ prepend to seq  
rule combine-seq-right: (combineSeq $t (seq $ts)) ~> (seq (cons $t $ts)) ;
-- unit is identity
rule combine-seq-unit-l: (combineSeq (unit) $t) ~> $t ;
rule combine-seq-unit-r: (combineSeq $t (unit)) ~> $t ;
-- default: make a new seq
rule combine-seq-default: (combineSeq $t1 $t2) ~> (seq (list $t1 $t2)) ;

-- splitSeq: Split first element from a sequence
rule split-seq: (splitSeq (seq (cons $h $rest))) ~> (pair $h (seq $rest)) ;
rule split-single: (splitSeq $t) ~> (pair $t (unit)) ;

-- wrapNode: Wrap a term with a node name
rule wrap-seq: (wrapNode $name (seq $ts)) ~> (con $name $ts) ;
rule wrap-other: (wrapNode $name $t) ~> (con $name (list $t)) ;

-- unwrapNode: Unwrap a node if name matches
rule unwrap-match: (unwrapNode $name (con $name $ts)) ~> (seq $ts) ;
rule unwrap-nomatch: (unwrapNode $name $t) ~> $t ;

-----------------------------------------------------
-- Tests (only Term syntax - not Pattern syntax)
-----------------------------------------------------

test "parse ident": x ;
test "parse string": "hello" ;
test "parse term var": x ;
test "parse term con": (f x y) ;
test "parse nested con": (app (lam x x) y) ;
